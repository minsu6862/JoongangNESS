plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.github.node-gradle.node' version '7.1.0' //react 빌드용 플러그인 추가
}

group = 'com.minsu'
version = '0.0.1-SNAPSHOT'
description = 'react_board_backend_project-20251013'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

/* ==================== React 프론트 엔드 빌드 설정 =======================*/

/* Node.js 빌드 설 */
node {
	version = '22.19.0' //Node.js 버전(현재 리액트 개발에 사용되었던 버전)
	download = true
   	workDir = file("${project.buildDir}/nodejs")
   	npmWorkDir = file("${project.buildDir}/npm")
}

//현재 react 소스가 저장되어 있는 폴더를 변수로 선언해서 build.gradle 내에서 사용
def frontendDir = "$projectDir/src/main/frontend"

//npm install 실행 작업 정의
tasks.register("npmCustomInstall", com.github.gradle.node.npm.task.NpmTask) {
   workingDir = file(frontendDir)
   args = ["install"]
}

//npm run build 실행 작업 정의 - npum run bulid 명령을 실행해서 React 빌드 결과물 build 폴더로 생성
tasks.register("npmBuild", com.github.gradle.node.npm.task.NpmTask) {
   dependsOn("npmCustomInstall")
   workingDir = file(frontendDir)
   args = ["run", "build"]
}

//React 빌드 결과를 Spring Boot로 복사
tasks.register("copyFrontend", Copy) {
   dependsOn("npmBuild")
   from("$frontendDir/build")
   into("$projectDir/src/main/resources/static")
}

//Srping boot 빌드 시 React 빌드도 함께 빌드되도록 자동 실행
tasks.named("processResources") {
   dependsOn("copyFrontend")
}